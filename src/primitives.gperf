%compare-strncmp
%includes
%language=ANSI-C
%readonly-tables
%struct-type

%{
#include <stdbool.h>

#include "analyzer.h"
#include "types.h"

#pragma GCC diagnostic ignored "-Wmissing-field-initializers"
#pragma GCC diagnostic ignored "-Wunused-parameter"
%}

struct typeslot { char *name; struct type inner; };
%%
i8,   { TYPE_NUM,  1, true  }
i16,  { TYPE_NUM,  2, true  }
i32,  { TYPE_NUM,  4, true  }
i64,  { TYPE_NUM,  8, true  }
i128, { TYPE_NUM, 16, true  }
int,  { TYPE_NUM,  8, true  }
u8,   { TYPE_NUM,  1, false }
u16,  { TYPE_NUM,  2, false }
u32,  { TYPE_NUM,  4, false }
u64,  { TYPE_NUM,  8, false }
u128, { TYPE_NUM, 16, false }
uint, { TYPE_NUM,  8, false }
rune, { TYPE_RUNE, 4, true  }
%%
const struct type *
typelookup(const uchar *p, size_t len)
{
	const struct typeslot *tp = in_word_set(p, len);
	return tp == NULL ? NULL : &tp->inner;
}
